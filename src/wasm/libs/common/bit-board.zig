//! ãƒ“ãƒƒãƒˆãƒœãƒ¼ãƒ‰

// std import
const std = @import("std");
const builtin = @import("builtin");
const testing = std.testing;
const assert = std.debug.assert;

// common import
const common = @import("./main.zig");
const types = common.types;

pub fn BitBoard(comptime height_arg: u16, comptime width_arg: u16) type {
    return struct {
        /// ãƒ“ãƒƒãƒˆãƒœãƒ¼ãƒ‰ã‚’è¡¨ã™å‹ã€‚
        const Self = @This();

        pub const height = height_arg;
        pub const width = width_arg;
        pub const size = height * width;

        const string_size = size + height - 1;
        const bit_length = std.math.log2_int_ceil(u16, size);

        /// ãƒ“ãƒƒãƒˆãƒœãƒ¼ãƒ‰ã®å‹ã€‚
        /// [é«˜ã•] Ã— [å¹…]ãƒ“ãƒƒãƒˆã€‚
        // pub const Board = std.bit_set.IntegerBitSet(size);
        // pub const Board = std.bit_set.ArrayBitSet(usize, size);
        pub const Board: type = std.bit_set.StaticBitSet(size);

        /// é«˜ã•ã®å‹ã€‚
        /// 0ã‹ã‚‰[é«˜ã•] - 1ãŒå…¨ã¦è¡¨ç¾ã§ãã‚‹æœ€å°ã®æ•´æ•°å‹ã€‚
        pub const Height: type = std.meta.Int(.unsigned, std.math.log2_int_ceil(u16, height));

        /// å¹…ã®å‹ã€‚
        /// 0ã‹ã‚‰[å¹…] - 1ãŒå…¨ã¦è¡¨ç¾ã§ãã‚‹æœ€å°ã®æ•´æ•°å‹ã€‚
        pub const Width: type = std.meta.Int(.unsigned, std.math.log2_int_ceil(u16, width));

        /// ãƒ“ãƒƒãƒˆãƒœãƒ¼ãƒ‰ã®ãƒ“ãƒƒãƒˆé•·ã•ã®å‹ã€‚
        /// 0ã‹ã‚‰[é«˜ã•] Ã— [å¹…] - 1ãŒå…¨ã¦è¡¨ç¾ã§ãã‚‹æœ€å°ã®æ•´æ•°å‹ã€‚
        pub const Index: type = std.meta.Int(.unsigned, bit_length);

        const UCharLength: type = std.meta.Int(.unsigned, std.math.log2_int_ceil(u16, string_size + 1));

        test "ğŸ“–BitBoard: ç¸¦ã¨æ¨ªã®ã‚µã‚¤ã‚ºã‹ã‚‰ãƒ“ãƒƒãƒˆã‚µã‚¤ã‚ºã‚’è¨ˆç®—ã—ã¦å‹ã‚’ä½œæˆã™ã‚‹" {
            {
                const B = BitBoard(8, 8);

                try testing.expectEqual(B.Board, std.bit_set.StaticBitSet(64));
                try testing.expectEqual(B.Height, u3);
                try testing.expectEqual(B.Width, u3);
                try testing.expectEqual(B.Index, u6);
            }

            {
                const B = BitBoard(9, 7);

                try testing.expectEqual(B.Board, std.bit_set.StaticBitSet(63));
                try testing.expectEqual(B.Height, u4);
                try testing.expectEqual(B.Width, u3);
                try testing.expectEqual(B.Index, u6);
            }
        }

        /// ãƒ“ãƒƒãƒˆãƒœãƒ¼ãƒ‰ã®å€¤
        board: Board,

        /// åº§æ¨™ã‚’ã‚¤ãƒ³ãƒ‡ãƒƒã‚¯ã‚¹ã«å¤‰æ›ã™ã‚‹é–¢æ•°
        /// åº§æ¨™ã¯(x, y)ã®çµ„ã€‚
        /// å·¦ä¸‹ãŒ(0, 0)ã€å³ã«ã„ãã¨xã€ä¸Šã«ã„ãã¨yãŒå¤§ãããªã‚‹ã€‚
        fn coordinateToIndex(x: Width, y: Height) Index {
            return x + (@as(Index, height) - y - 1) * @as(Index, width);
        }

        /// ç©ºã®ãƒœãƒ¼ãƒ‰ã‚’ä½œæˆã™ã‚‹ã€‚
        pub fn init() Self {
            return .{ .board = Board.initEmpty() };
        }

        /// æŒ‡å®šã—ãŸåº§æ¨™ã®ãƒ“ãƒƒãƒˆ1ã¤ã®ã¿ãŒã‚ªãƒ³ã®ãƒ“ãƒƒãƒˆãƒœãƒ¼ãƒ‰ã‚’ä½œæˆã™ã‚‹ã€‚
        pub fn initWithCoordinate(x: Width, y: Height) Self {
            return initWithIndex(coordinateToIndex(x, y));
        }

        test "ğŸ“–BitBoard.initWithCoordinate: åº§æ¨™ã‹ã‚‰ãã“ã ã‘ãƒ“ãƒƒãƒˆã®ç«‹ã£ãŸãƒœãƒ¼ãƒ‰ã‚’ä½œæˆã™ã‚‹" {
            // + 0 1 2 3
            // 3 0 1 2 3
            // 2 4 5 6 7
            // 1 8 9 a b
            // 0 c d e f

            const B = BitBoard(4, 4);

            {
                const board1 = B.initWithCoordinate(0, 0);
                const board2 = B.initWithString(
                    \\....
                    \\....
                    \\....
                    \\o...
                , 'o');

                try testing.expectEqual(board1, board2);
            }

            {
                const board1 = B.initWithCoordinate(1, 0);
                const board2 = B.initWithString(
                    \\....
                    \\....
                    \\....
                    \\.o..
                , 'o');

                try testing.expectEqual(board1, board2);
            }

            {
                const board1 = B.initWithCoordinate(0, 2);
                const board2 = B.initWithString(
                    \\....
                    \\o...
                    \\....
                    \\....
                , 'o');

                try testing.expectEqual(board1, board2);
            }

            {
                const board1 = B.initWithCoordinate(3, 3);
                const board2 = B.initWithString(
                    \\...o
                    \\....
                    \\....
                    \\....
                , 'o');

                try testing.expectEqual(board1, board2);
            }
        }

        // æ–‡å­—åˆ—ã‚’ä½¿ã†é–¢æ•°ã€‚

        /// ç•ªå·ã‹ã‚‰ãƒœãƒ¼ãƒ‰ã‚’ä½œæˆã™ã‚‹ã€‚
        pub fn initWithIndex(index: usize) Self {
            var board = Board.initEmpty();
            board.set(index);
            return .{ .board = board };
        }

        /// ãƒ“ãƒƒãƒˆã®æ•´æ•°è¡¨ç¾ã‹ã‚‰å¤‰æ›ã™ã‚‹ã€‚
        pub fn initWithInteger(int: std.meta.Int(.unsigned, size)) Self {
            if (Board == std.bit_set.IntegerBitSet(size)) {
                return .{ .board = .{ .mask = int } };
            } else if (Board == std.bit_set.ArrayBitSet(usize, size)) {
                const usize_bits = @bitSizeOf(usize);
                const num_masks = (size + usize_bits - 1) / usize_bits;
                const masks_type = [num_masks]usize;
                const int_masks: masks_type = @bitCast(@as(std.meta.Int(.unsigned, @bitSizeOf(masks_type)), int));
                return .{ .board = .{ .masks = int_masks } };
            } else {
                @compileError("board is static bitset");
            }
        }

        /// ãƒ“ãƒƒãƒˆãƒœãƒ¼ãƒ‰ã®æ–‡å­—åˆ—ã‹ã‚‰ãƒ“ãƒƒãƒˆãƒœãƒ¼ãƒ‰ã‚’ä½œæˆã™ã‚‹ã€‚
        /// [å¹…]æ–‡å­—ã®å¾Œ'\n'ã‚’[é«˜ã•]å›ç¹°ã‚Šè¿”ã™ã€‚
        /// æœ€å¾Œã«ã¯'\n'ã‚’ã¤ã‘ãªã„ã€‚
        /// ç¬¬äºŒå¼•æ•°ã§çŸ³ã‚’ç¤ºã™æ–‡å­—ã‚’æŒ‡å®šã™ã‚‹ã€‚
        ///
        /// ç¯„å›²å¤–ã®ã‚‚ã®ãŒ1ã¤ã§ã‚‚ã‚ã‚‹ã¨0ã‚’è¿”ã™
        pub fn initWithString(str: []const u8, piece_symbol: u8) Self {
            assert(str.len == string_size);

            var board: Board = Board.initEmpty();
            var char_count: UCharLength = 0;
            var bit_count: Index = 0;

            for (0..height) |n| {
                for (0..width) |_| {
                    const char = str[char_count];

                    if (char == piece_symbol) {
                        board.set(bit_count);
                    }

                    char_count += 1;
                    bit_count +|= 1;
                }

                if (n == height - 1) {
                    break;
                }

                assert(str[char_count] == '\n');
                char_count += 1;
            }

            return .{ .board = board };
        }

        test "ğŸ“–BitBoard.initWithString: æ–‡å­—åˆ—ã‹ã‚‰ãƒœãƒ¼ãƒ‰ã‚’ä½œæˆã™ã‚‹" {
            const B = BitBoard(4, 4);

            const board = B.initWithString(
                \\.o.o
                \\....
                \\....
                \\....
            , 'o');

            try testing.expectEqual(board, B.initWithInteger(0b0000_0000_0000_1010));
        }

        pub const west_mask = blk: {
            var board = init();

            for (0..height) |y| {
                board.setToggleCoordinate(0, y);
            }

            break :blk board.getInverted();
        };

        test "ğŸ“–BitBoard.west_mask: å·¦ç«¯ã®ã¿0ã®ãƒœãƒ¼ãƒ‰" {
            const B = BitBoard(3, 3);

            try B.west_mask.expect(
                \\.oo
                \\.oo
                \\.oo
            );
        }

        pub const east_mask = blk: {
            var board = init();

            for (0..height) |y| {
                board.setToggleCoordinate(width - 1, y);
            }

            break :blk board.getInverted();
        };

        test "ğŸ“–BitBoard.east_mask: å³ç«¯ã®ã¿0ã®ãƒœãƒ¼ãƒ‰" {
            const B = BitBoard(3, 3);

            try B.east_mask.expect(
                \\oo.
                \\oo.
                \\oo.
            );
        }

        pub const north_mask = blk: {
            var board = init();

            for (0..width) |x| {
                board.setToggleCoordinate(x, height - 1);
            }

            break :blk board.getInverted();
        };

        test "ğŸ“–BitBoard.north_mask: ä¸Šç«¯ã®ã¿0ã®ãƒœãƒ¼ãƒ‰" {
            const B = BitBoard(3, 3);

            try B.north_mask.expect(
                \\...
                \\ooo
                \\ooo
            );
        }

        pub const south_mask = blk: {
            var board = init();

            for (0..width) |x| {
                board.setToggleCoordinate(x, 0);
            }

            break :blk board.getInverted();
        };

        test "ğŸ“–BitBoard.south_mask: ä¸‹ç«¯ã®ã¿0ã®ãƒœãƒ¼ãƒ‰" {
            const B = BitBoard(3, 3);

            try B.south_mask.expect(
                \\ooo
                \\ooo
                \\...
            );
        }

        pub fn eql(self: Self, other: Self) bool {
            return self.board.eql(other.board);
        }

        /// ãƒœãƒ¼ãƒ‰ãŒç©ºã‹ã©ã†ã‹åˆ¤å®šã™ã‚‹ã€‚
        /// `self == 0`
        pub fn isEmpty(self: Self) bool {
            return self.board.eql(Board.initEmpty());
        }

        /// 2ã¤ã®ãƒœãƒ¼ãƒ‰ãŒé‡ãªã‚Šã‚’æŒãŸãªã„ã‹ã©ã†ã‹åˆ¤å®šã™ã‚‹ã€‚
        /// `self & mask != 0`
        pub fn isJoint(self: Self, mask: Self) bool {
            return !self.isDisjoint(mask);
        }

        /// 2ã¤ã®ãƒœãƒ¼ãƒ‰ãŒé‡ãªã‚Šã‚’æŒãŸãªã„ã‹ã©ã†ã‹åˆ¤å®šã™ã‚‹ã€‚
        /// `self & mask == 0`
        pub fn isDisjoint(self: Self, mask: Self) bool {
            return self.masks(mask).isEmpty();
        }

        ///  ãƒ“ãƒƒãƒˆã®ã‚ã‚‹ãƒã‚¹ã‚’æ•°ãˆã‚‹ã€‚
        pub fn count(self: Self) usize {
            return self.board.count();
        }

        /// board << length
        pub fn shl(self: Self, length: usize) Self {
            assert(length <= size);

            const mask_int = self.toInteger();
            const shifted_mask = mask_int << @intCast(length);
            return initWithInteger(shifted_mask);
        }

        test "ğŸ“–BitBoard.shl: ãƒœãƒ¼ãƒ‰ã®å·¦ã‚·ãƒ•ãƒˆã—ãŸãƒœãƒ¼ãƒ‰ã‚’å¾—ã‚‹" {
            const B = BitBoard(16, 16);

            const board = B.initWithString(
                \\o...............
                \\.o............o.
                \\................
                \\...............o
                \\o...............
                \\.o..............
                \\..............o.
                \\...............o
                \\o...............
                \\..............o.
                \\.o..............
                \\...............o
                \\o...............
                \\................
                \\.o............o.
                \\...............o
            , 'o');

            try board.shl(1).expect(
                \\.o..............
                \\..o............o
                \\................
                \\................
                \\oo..............
                \\..o.............
                \\...............o
                \\................
                \\oo..............
                \\...............o
                \\..o.............
                \\................
                \\oo..............
                \\................
                \\..o............o
                \\................
            );

            try board.shl(64).expect(
                \\................
                \\................
                \\................
                \\................
                \\o...............
                \\.o............o.
                \\................
                \\...............o
                \\o...............
                \\.o..............
                \\..............o.
                \\...............o
                \\o...............
                \\..............o.
                \\.o..............
                \\...............o
            );
        }

        /// board >> length
        pub fn shr(self: Self, length: usize) Self {
            assert(length <= size);

            const mask_int = self.toInteger();
            const shifted_mask = mask_int >> @intCast(length);
            return initWithInteger(shifted_mask);
        }

        test "ğŸ“–BitBoard.shr: ãƒœãƒ¼ãƒ‰ã®å³ã‚·ãƒ•ãƒˆã—ãŸãƒœãƒ¼ãƒ‰ã‚’å¾—ã‚‹" {
            const B = BitBoard(16, 16);

            const board = B.initWithString(
                \\o...............
                \\.o............o.
                \\................
                \\...............o
                \\o...............
                \\.o..............
                \\..............o.
                \\...............o
                \\o...............
                \\..............o.
                \\.o..............
                \\...............o
                \\o...............
                \\................
                \\.o............o.
                \\...............o
            , 'o');

            try board.shr(1).expect(
                \\................
                \\o............o..
                \\................
                \\..............oo
                \\................
                \\o...............
                \\.............o..
                \\..............oo
                \\................
                \\.............o..
                \\o...............
                \\..............oo
                \\................
                \\................
                \\o............o..
                \\..............o.
            );

            try board.shr(64).expect(
                \\o...............
                \\.o..............
                \\..............o.
                \\...............o
                \\o...............
                \\..............o.
                \\.o..............
                \\...............o
                \\o...............
                \\................
                \\.o............o.
                \\...............o
                \\................
                \\................
                \\................
                \\................
            );
        }

        /// selfã«otherã®ãƒ“ãƒƒãƒˆã‚’è¶³ã—åˆã‚ã›ã‚‹ã€‚
        /// `self |= other;`
        pub fn setUnion(self: *Self, other: Self) void {
            self.board.setUnion(other.board);
        }

        /// selfã«otherã®ãƒ“ãƒƒãƒˆã‚’è¶³ã—åˆã‚ã›ãŸãƒ“ãƒƒãƒˆãƒœãƒ¼ãƒ‰ã‚’è¿”ã™ã€‚
        /// `self | other;`
        pub fn unions(self: Self, other: Self) Self { // unionã¯äºˆç´„èª
            return .{
                .board = self.board.unionWith(other.board),
            };
        }

        /// selfã«otherã®ãƒ“ãƒƒãƒˆã‚’ãƒã‚¹ã‚¯ã™ã‚‹ã€‚
        /// `self &= other;`
        pub fn setMask(self: *Self, other: Self) void {
            self.board.setIntersection(other.board);
        }

        /// selfã«otherã®ãƒ“ãƒƒãƒˆã‚’ãƒã‚¹ã‚¯ã—ãŸãƒ“ãƒƒãƒˆãƒœãƒ¼ãƒ‰ã‚’è¿”ã™ã€‚
        /// `self & other;`
        pub fn masks(self: Self, other: Self) Self {
            return .{
                .board = self.board.intersectWith(other.board),
            };
        }

        /// selfã«otherã®ãƒ“ãƒƒãƒˆã‚’åè»¢ã™ã‚‹ã€‚
        /// `self ^= other;`
        pub fn setToggle(self: *Self, other: Self) void {
            self.board.toggleSet(other.board);
        }

        /// selfã«otherã®ãƒ“ãƒƒãƒˆã‚’åè»¢ã—ãŸãƒ“ãƒƒãƒˆãƒœãƒ¼ãƒ‰ã‚’è¿”ã™ã€‚
        /// `self ^ other;`
        pub fn toggled(self: Self, other: Self) Self {
            return .{
                .board = self.board.xorWith(other.board),
            };
        }

        /// selfã®indexç•ªç›®ã®ãƒ“ãƒƒãƒˆã‚’åè»¢ã™ã‚‹ã€‚
        /// `self ^= 1 << index;`
        pub fn setToggleIndex(self: *Self, index: usize) void {
            self.board.set(index);
        }

        /// selfã®(x,y)ã®ãƒ“ãƒƒãƒˆã‚’åè»¢ã™ã‚‹ã€‚
        pub fn setToggleCoordinate(self: *Self, x: Width, y: Height) void {
            self.setToggleIndex(coordinateToIndex(x, y));
        }

        /// ã™ã¹ã¦ã®ãƒ“ãƒƒãƒˆã‚’åè»¢ã™ã‚‹ã€‚
        /// `self = ~self;`
        pub fn invert(self: *Self) void {
            self.board.toggleAll();
        }

        /// ã™ã¹ã¦ã®ãƒ“ãƒƒãƒˆã‚’ãƒã‚¹ã‚¯ã—ãŸãƒ“ãƒƒãƒˆãƒœãƒ¼ãƒ‰ã‚’è¿”ã™ã€‚
        /// `~self;`
        pub fn getInverted(self: Self) Self {
            return .{
                .board = self.board.complement(),
            };
        }

        /// selfã‹ã‚‰otherã®ãƒ“ãƒƒãƒˆã‚’é™¤å¤–ã—ãŸãƒ“ãƒƒãƒˆãƒœãƒ¼ãƒ‰ã‚’è¿”ã™ã€‚
        /// `self & ~other;`
        pub fn excludes(self: Self, other: Self) Self {
            return .{
                .board = self.board.differenceWith(other.board),
            };
        }

        /// 8æ–¹å‘
        pub const Direction = enum { n, s, e, w, nw, ne, sw, se };

        /// æ–¹å‘ã«å¯¾å¿œã™ã‚‹ç§»å‹•è·é›¢ã‚’è¨ˆç®—ã™ã‚‹ã€‚
        fn moveLength(direction: Direction) Index {
            return switch (direction) {
                .n, .s => width,
                .e, .w => 1,
                .ne, .sw => width - 1,
                .nw, .se => width + 1,
            };
        }

        /// ãƒœãƒ¼ãƒ‰ã®å…¨ã¦ã®ãƒ“ãƒƒãƒˆã‚’ç§»å‹•ã™ã‚‹ã€‚
        pub fn move(self: Self, direction: Direction) Self {
            const length = moveLength(direction);

            switch (direction) {
                .n, .w, .ne, .nw => return self.shr(length),
                .s, .e, .se, .sw => return self.shl(length),
            }
        }

        test "ğŸ“–BitBoard.move: ãƒœãƒ¼ãƒ‰ã®é§’ã‚’1ã¤å‹•ã‹ã—ãŸãƒœãƒ¼ãƒ‰ã‚’å¾—ã‚‹" {
            const B = BitBoard(3, 3);

            const board = B.initWithString(
                \\...
                \\.o.
                \\...
            , 'o');

            try board.move(.n).expect(
                \\.o.
                \\...
                \\...
            );

            try board.move(.s).expect(
                \\...
                \\...
                \\.o.
            );

            try board.move(.e).expect(
                \\...
                \\..o
                \\...
            );

            try board.move(.w).expect(
                \\...
                \\o..
                \\...
            );

            try board.move(.ne).expect(
                \\..o
                \\...
                \\...
            );

            try board.move(.nw).expect(
                \\o..
                \\...
                \\...
            );

            try board.move(.se).expect(
                \\...
                \\...
                \\..o
            );

            try board.move(.sw).expect(
                \\...
                \\...
                \\o..
            );
        }

        /// ãƒœãƒ¼ãƒ‰ã®å…¨ã¦ã®ãƒ“ãƒƒãƒˆã‚’ç§»å‹•ã™ã‚‹ã€‚
        /// å³ç«¯â†’å·¦ç«¯ã€å·¦ç«¯â†’å³ç«¯ã®ç§»å‹•ã‚’åˆ¶å¾¡ã™ã‚‹ã€‚
        pub fn moveMasked(self: Self, direction: Direction) Self {
            const length = moveLength(direction);

            switch (direction) {
                .n => return self.shr(length),
                .w, .nw => return self.masks(west_mask).shr(length),
                .ne => return self.masks(east_mask).shr(length),
                .s => return self.shl(length),
                .e, .se => return self.masks(east_mask).shl(length),
                .sw => return self.masks(west_mask).shl(length),
            }
        }

        test "ğŸ“–BitBoard.moveMasked: ç«¯ã‹ã‚‰ç«¯ã¸ã®ç§»å‹•ãŒã§ããªã„" {
            const B = BitBoard(3, 3);

            {
                const board = B.initWithString(
                    \\..o
                    \\...
                    \\o..
                , 'o');

                try board.move(.e).expect(
                    \\...
                    \\o..
                    \\.o.
                );

                try board.moveMasked(.e).expect(
                    \\...
                    \\...
                    \\.o.
                );
            }

            {
                const board = B.initWithString(
                    \\...
                    \\oo.
                    \\...
                , 'o');

                try board.move(.sw).expect(
                    \\...
                    \\..o
                    \\o..
                );

                try board.moveMasked(.sw).expect(
                    \\...
                    \\...
                    \\o..
                );
            }
        }

        /// ãƒœãƒ¼ãƒ‰ã®å…¨ã¦ã®ãƒ“ãƒƒãƒˆã‚’è¤‡æ•°ã®æ–¹å‘ã«ç§»å‹•ã™ã‚‹ã€‚
        pub fn moveMultiple(self: Self, directions: []const Direction) Self {
            var result = init();

            for (directions) |direction| {
                result.setUnion(self.move(direction));
            }

            return result;
        }

        /// ãƒœãƒ¼ãƒ‰ã®å…¨ã¦ã®ãƒ“ãƒƒãƒˆã‚’è¤‡æ•°ã®æ–¹å‘ã«ç§»å‹•ã™ã‚‹ã€‚
        /// å³ç«¯â†’å·¦ç«¯ã€å·¦ç«¯â†’å³ç«¯ã®ç§»å‹•ã‚’åˆ¶å¾¡ã™ã‚‹ã€‚
        pub fn moveMaskedMultiple(self: Self, directions: []const Direction) Self {
            var result = init();

            for (directions) |direction| {
                result.setUnion(self.moveMasked(direction));
            }

            return result;
        }

        /// next()ã®è¿”ã‚Šå€¤ã¯ãƒ“ãƒƒãƒˆã®ã‚¤ãƒ³ãƒ‡ãƒƒã‚¯ã‚¹ã€‚
        /// `BitBoard.initWithIndex`ã§å¾©å…ƒã§ãã‚‹ã€‚
        pub inline fn iterator(self: Self) Board.Iterator(.{}) {
            return self.board.iterator(.{});
        }

        test "ğŸ“–BitBoard.Iterator: ãƒœãƒ¼ãƒ‰ã®ONã®å„ãƒ“ãƒƒãƒˆã‚’ç¹°ã‚Šè¿”ã™" {
            const B = BitBoard(4, 4);

            const board: B = B.initWithString(
                \\.o..
                \\..o.
                \\o...
                \\...o
            , 'o');

            var it = B.iterator(board);

            try B.expect(B.initWithIndex(it.next().?),
                \\.o..
                \\....
                \\....
                \\....
            );
            try B.expect(B.initWithIndex(it.next().?),
                \\....
                \\..o.
                \\....
                \\....
            );
            try B.expect(B.initWithIndex(it.next().?),
                \\....
                \\....
                \\o...
                \\....
            );
            try B.expect(B.initWithIndex(it.next().?),
                \\....
                \\....
                \\....
                \\...o
            );

            try testing.expectEqual(it.next(), null);
        }

        /// ãƒœãƒ¼ãƒ‰ã‚’æ•´æ•°ã«å¤‰æ›ã™ã‚‹
        pub fn toInteger(self: Self) std.meta.Int(.unsigned, size) {
            switch (Board) {
                std.bit_set.IntegerBitSet(size) => {
                    return self.board.mask;
                },
                std.bit_set.ArrayBitSet(usize, size) => {
                    const mask = self.board.masks;
                    const masks_int: std.meta.Int(.unsigned, @bitSizeOf(@TypeOf(mask))) = @bitCast(mask);
                    return @intCast(masks_int);
                },
                else => {
                    @compileError("board is static bitset");
                },
            }
        }

        pub fn toIndexInteger(self: Self) Index {
            const int = self.toInteger();

            return @intCast(@ctz(int));
        }

        test "ğŸ“–BitBoard.toIndexInteger: å¾©å…ƒå¯èƒ½ãªã‚¤ãƒ³ãƒ‡ãƒƒã‚¯ã‚¹ã‚’ä½œæˆã™ã‚‹" {
            const B = BitBoard(16, 16);

            const board = B.initWithIndex(5);

            const index_integer = board.toIndexInteger();

            try std.testing.expectEqual(5, index_integer);
            try std.testing.expect(board.eql(B.initWithIndex(index_integer)));
        }

        pub fn toCoordinate(self: Self) struct { x: Width, y: Height } {
            const int = self.toIndexInteger();

            const x: Width = @intCast(int % @as(Index, width));
            const y: Height = @intCast(@as(Index, height) - @divTrunc(int, @as(Index, width)) - 1);

            return .{ .x = x, .y = y };
        }

        test "ğŸ“–BitBoard.toCoordinate: å¾©å…ƒå¯èƒ½ãªåº§æ¨™ã®çµ„ã‚’ä½œæˆã™ã‚‹" {
            const B = BitBoard(16, 16);

            const board = B.initWithCoordinate(3, 5);

            const coord = board.toCoordinate();

            try std.testing.expectEqual(3, coord.x);
            try std.testing.expectEqual(5, coord.y);
            try std.testing.expect(board.eql(B.initWithCoordinate(coord.x, coord.y)));
        }

        /// ãƒ“ãƒƒãƒˆãƒœãƒ¼ãƒ‰ã‚’æ–‡å­—åˆ—ã«å¤‰æ›ã™ã‚‹ã€‚
        pub fn toString(self: Self, piece_symbol: u8, empty_symbol: u8) [string_size]u8 {
            var str: [string_size]u8 = .{empty_symbol} ** string_size;

            var char_count: UCharLength = 0;
            var bit_count: Index = 0;
            for (0..height) |l| {
                for (0..width) |_| {
                    if (self.board.isSet(bit_count)) {
                        str[char_count] = piece_symbol;
                    }

                    char_count += 1;
                    bit_count +|= 1;
                }

                if (l == height - 1) {
                    break;
                }

                str[char_count] = '\n';
                char_count += 1;
            }

            return str;
        }

        test "ğŸ“–BitBoard.toString: ãƒœãƒ¼ãƒ‰ã‹ã‚‰æ–‡å­—åˆ—ã«å¤‰æ›ã™ã‚‹" {
            const B = BitBoard(4, 4);

            const board = B.initWithString(
                \\o.oo
                \\oo.o
                \\ooo.
                \\oooo
            , 'o');

            try testing.expectEqualStrings(
                &B.toString(board, 'o', '.'),
                \\o.oo
                \\oo.o
                \\ooo.
                \\oooo
                ,
            );
        }

        pub fn format(value: @This(), comptime fmt: []const u8, options: std.fmt.FormatOptions, writer: anytype) !void {
            _ = fmt;
            _ = options;

            return writer.print("{s}", .{value.toString('o', '.')});
        }

        pub fn expect(self: Self, expected: []const u8) error{TestExpectedEqual}!void {
            const expected_board = initWithString(expected, 'o');

            if (self.eql(expected_board)) {
                return;
            }

            std.debug.print(
                \\
                \\- expected --
                \\{s}
                \\
                \\- actual ----
                \\{s}
                \\
            , .{
                expected_board.toString('o', '.'),
                self.toString('o', '.'),
            });

            return error.TestExpectedEqual;
        }

        pub fn expectJoint(self: Self, expected: []const u8) error{TestExpectedEqual}!void {
            const expected_board = initWithString(expected, 'o');

            if (self.isJoint(expected_board)) {
                return;
            }

            std.debug.print(
                \\
                \\- expected --
                \\{s}
                \\
                \\- actual ----
                \\{s}
                \\
            , .{
                expected_board.toString('o', '.'),
                self.toString('o', '.'),
            });

            return error.TestExpectedEqual;
        }
    };
}
