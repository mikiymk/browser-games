const std = @import("std");
const builtin = @import("builtin");
const Allocator = std.mem.Allocator;
const EnumArray = std.enums.EnumArray;

const draughts = @import("../english-draughts/main.zig");
const Game = draughts.Game;
const Board = draughts.Board;
const BitBoard = draughts.BitBoard;
const Color = Game.Color;
const Piece = Game.Piece;

// common import
const common = @import("../common/main.zig");

const ColorBoard = EnumArray(Color, BitBoard);
const PieceColorBoard = EnumArray(Piece, ColorBoard);

boards: PieceColorBoard,

/// ãƒœãƒ¼ãƒ‰ã‚’ä½œæˆã™ã‚‹ã€‚
pub fn init(a: Allocator, board_white: BitBoard, board_black: BitBoard) @This() {
    _ = a;

    return .{ .boards = PieceColorBoard.init(.{
        .pawn = ColorBoard.init(.{
            .white = board_white,
            .black = board_black,
        }),
        .king = ColorBoard.initFill(BitBoard.init()),
    }) };
}

/// æ–‡å­—åˆ—ã‹ã‚‰ãƒœãƒ¼ãƒ‰ã‚’ä½œæˆã™ã‚‹ã€‚
/// ç™½ã¯oã€é»’ã¯xã§æŒ‡å®šã™ã‚‹ã€‚
pub fn initWithString(a: Allocator, str: []const u8) @This() {
    const mark_white = 'o';
    const mark_black = 'x';

    const board_white = BitBoard.initWithString(str, mark_white);
    const board_black = BitBoard.initWithString(str, mark_black);

    return init(a, board_white, board_black);
}

/// æŒ‡å®šã—ãŸè‰²ã®ãƒœãƒ¼ãƒ‰çŠ¶æ…‹ã‚’å–å¾—ã™ã‚‹
pub fn getBoard(self: Board, color: Color, piece: Piece) BitBoard {
    return self.boards.get(piece).get(color);
}

/// æŒ‡å®šã—ãŸè‰²ã®ãƒœãƒ¼ãƒ‰çŠ¶æ…‹ã‚’å–å¾—ã™ã‚‹
pub fn getBoardPtr(self: *Board, color: Color, piece: Piece) *BitBoard {
    return self.boards.getPtr(piece).getPtr(color);
}

/// æŒ‡å®šã—ãŸå ´æ‰€ã®é§’ã®è‰²ã¨ç¨®é¡ã‚’å–å¾—ã™ã‚‹
pub fn getColorPiece(self: Board, position: BitBoard) ?struct { Color, Piece } {
    const white_pawn = self.getBoard(.white, .pawn);
    const white_king = self.getBoard(.white, .king);
    const black_pawn = self.getBoard(.black, .pawn);
    const black_king = self.getBoard(.black, .king);

    if (position.isJoint(white_pawn)) {
        return .{ .white, .pawn };
    } else if (position.isJoint(white_king)) {
        return .{ .white, .king };
    } else if (position.isJoint(black_pawn)) {
        return .{ .black, .pawn };
    } else if (position.isJoint(black_king)) {
        return .{ .black, .king };
    } else {
        return null;
    }
}

/// æŒ‡å®šã—ãŸå ´æ‰€ã®é§’ã®è‰²ã‚’å–å¾—ã™ã‚‹
pub fn getColor(self: Board, position: BitBoard) ?Color {
    return (self.getColorPiece(position) orelse return null)[0];
}

test "ğŸ“–Board.getColor" {
    const a = std.testing.allocator;
    const board_str =
        \\........
        \\........
        \\.....o..
        \\........
        \\........
        \\..x.....
        \\........
        \\........
    ;

    const board = Board.initWithString(a, board_str);

    try std.testing.expectEqual(.white, board.getColor(BitBoard.initWithCoordinate(5, 5)));
    try std.testing.expectEqual(.black, board.getColor(BitBoard.initWithCoordinate(2, 2)));
    try std.testing.expectEqual(null, board.getColor(BitBoard.initWithCoordinate(4, 4)));
}

/// æŒ‡å®šã—ãŸå ´æ‰€ã®é§’ã®ç¨®é¡ã‚’å–å¾—ã™ã‚‹
pub fn getPiece(self: Board, position: BitBoard) ?Piece {
    return (self.getColorPiece(position) orelse return null)[1];
}

/// ç›¤ä¸Šã®é€šå¸¸ç§»å‹•ã‚’å…¨ã¦å–å¾—ã™ã‚‹ã€‚
pub fn getAllWalkMoves(self: Board, a: Allocator, color: Color) ![]Game.Move {
    var walk_moves = std.ArrayList(Game.Move).init(a);
    errdefer walk_moves.deinit();

    // ãƒãƒ¼ãƒ³ã®ç§»å‹•ã‚’å–å¾—
    const pawn_board = self.getBoard(color, .pawn);
    var pawn_iterator = pawn_board.iterator();
    while (pawn_iterator.next()) |pawn_position_index| {
        const pawn_position = BitBoard.initWithIndex(pawn_position_index);
        const walk_to = self.movedPawnWalk(pawn_position);

        var walk_to_iterator = walk_to.iterator();
        while (walk_to_iterator.next()) |walk_to_position_index| {
            const move = Game.Move.init(pawn_position_index, walk_to_position_index);
            try walk_moves.append(move);
        }
    }

    // ã‚­ãƒ³ã‚°ã®ç§»å‹•ã‚’å–å¾—
    const king_board = self.getBoard(color, .king);
    var king_iterator = king_board.iterator();
    while (king_iterator.next()) |king_position_index| {
        const king_position = BitBoard.initWithIndex(king_position_index);
        const walk_to = self.movedKingWalk(king_position);

        var walk_to_iterator = walk_to.iterator();
        while (walk_to_iterator.next()) |walk_to_position_index| {
            const move = Game.Move.init(king_position_index, walk_to_position_index);
            try walk_moves.append(move);
        }
    }

    return walk_moves.toOwnedSlice();
}

test "ğŸ“–Board.getAllWalkMoves" {
    const a = std.testing.allocator;
    const board_str =
        \\........
        \\......o.
        \\........
        \\........
        \\...x....
        \\..o.....
        \\........
        \\........
    ;

    const board = Board.initWithString(a, board_str);
    const moves = try board.getAllWalkMoves(a, .white);
    defer a.free(moves);

    const expected = [_]Game.Move{
        Game.Move.init(BitBoard.indexFromCoordinate(6, 6), BitBoard.indexFromCoordinate(5, 7)),
        Game.Move.init(BitBoard.indexFromCoordinate(6, 6), BitBoard.indexFromCoordinate(7, 7)),
        Game.Move.init(BitBoard.indexFromCoordinate(2, 2), BitBoard.indexFromCoordinate(1, 3)),
    };

    try std.testing.expectEqual(expected.len, moves.len);
    for (expected) |expected_move| {
        for (moves) |move| {
            if (expected_move.eql(move)) {
                break;
            }
        } else {
            return error.NotContain;
        }
    }
}

/// ç›¤ä¸Šã®ã‚¸ãƒ£ãƒ³ãƒ—ã‚’å…¨ã¦å–å¾—ã™ã‚‹ã€‚
pub fn getAllJumpMoves(self: Board, a: Allocator, color: Color) ![]Game.Move {
    var jump_moves = std.ArrayList(Game.Move).init(a);
    errdefer jump_moves.deinit();

    // ãƒãƒ¼ãƒ³ã®ç§»å‹•ã‚’å–å¾—
    const pawn_board = self.getBoard(color, .pawn);
    var pawn_iterator = pawn_board.iterator();
    while (pawn_iterator.next()) |pawn_position_index| {
        const pawn_position = BitBoard.initWithIndex(pawn_position_index);
        const jump_to = self.movedPawnJump(pawn_position);

        var jump_to_iterator = jump_to.iterator();
        while (jump_to_iterator.next()) |jump_to_position_index| {
            const move = Game.Move.init(pawn_position_index, jump_to_position_index);
            try jump_moves.append(move);
        }
    }

    // ã‚­ãƒ³ã‚°ã®ç§»å‹•ã‚’å–å¾—
    const king_board = self.getBoard(color, .king);
    var king_iterator = king_board.iterator();
    while (king_iterator.next()) |king_position_index| {
        const king_position = BitBoard.initWithIndex(king_position_index);
        const jump_to = self.movedKingJump(king_position);

        var jump_to_iterator = jump_to.iterator();
        while (jump_to_iterator.next()) |jump_to_position_index| {
            const move = Game.Move.init(king_position_index, jump_to_position_index);
            try jump_moves.append(move);
        }
    }

    return jump_moves.toOwnedSlice();
}

test "ğŸ“–Board.getAllJumpMoves" {
    const a = std.testing.allocator;
    const board_str =
        \\........
        \\......o.
        \\........
        \\........
        \\...x....
        \\..o.....
        \\........
        \\........
    ;

    const board = Board.initWithString(a, board_str);
    const moves = try board.getAllJumpMoves(a, .white);
    defer a.free(moves);

    const expected = [_]Game.Move{
        Game.Move.init(BitBoard.indexFromCoordinate(2, 2), BitBoard.indexFromCoordinate(4, 4)),
    };

    try std.testing.expectEqual(expected.len, moves.len);
    for (expected) |expected_move| {
        for (moves) |move| {
            if (expected_move.eql(move)) {
                break;
            }
        } else {
            return error.NotContain;
        }
    }
}

fn pawnDirections(color: Color) [2]BitBoard.Direction {
    return switch (color) {
        .white => [2]BitBoard.Direction{ .ne, .nw },
        .black => [2]BitBoard.Direction{ .se, .sw },
    };
}

/// æŒ‡å®šã—ãŸåº§æ¨™ã®ãƒãƒ¼ãƒ³ã®é§’ãŒç§»å‹•ã§ãã‚‹ç¯„å›²ã‚’å–å¾—ã™ã‚‹ã€‚
pub fn movedPawnWalk(self: Board, position: BitBoard) BitBoard {
    const color = self.getColor(position) orelse return BitBoard.init();

    const ally_pawn = self.getBoard(color, .pawn);
    const ally_king = self.getBoard(color, .king);
    const opponent_pawn = self.getBoard(color.turn(), .pawn);
    const opponent_king = self.getBoard(color.turn(), .king);

    const ally_board = ally_pawn.unions(ally_king);
    const opponent_board = opponent_pawn.unions(opponent_king);
    const occupied_positions = ally_board.unions(opponent_board);

    const move_to = position.moveMaskedMultiple(&pawnDirections(color));

    return move_to.excludes(occupied_positions);
}

test "ğŸ“–Board.movedPawnWalk" {
    const a = std.testing.allocator;
    const board_str =
        \\.....o.x
        \\......o.
        \\.x......
        \\........
        \\........
        \\......x.
        \\.o...x.o
        \\........
    ;

    const board = Board.initWithString(a, board_str);

    // ãƒãƒ¼ãƒ³ã¯å‰æ–¹ã«ã®ã¿ç§»å‹•ã§ãã‚‹
    try board.movedPawnWalk(BitBoard.initWithCoordinate(1, 1)).expect(
        \\........
        \\........
        \\........
        \\........
        \\........
        \\o.o.....
        \\........
        \\........
    );

    try board.movedPawnWalk(BitBoard.initWithCoordinate(1, 5)).expect(
        \\........
        \\........
        \\........
        \\o.o.....
        \\........
        \\........
        \\........
        \\........
    );

    // ç§»å‹•å…ˆã«é§’ãŒã‚ã‚‹å ´åˆã¯ç§»å‹•ã§ããªã„
    try board.movedPawnWalk(BitBoard.initWithCoordinate(6, 6)).expect(
        \\........
        \\........
        \\........
        \\........
        \\........
        \\........
        \\........
        \\........
    );

    try board.movedPawnWalk(BitBoard.initWithCoordinate(6, 2)).expect(
        \\........
        \\........
        \\........
        \\........
        \\........
        \\........
        \\........
        \\........
    );
}

/// æŒ‡å®šã—ãŸåº§æ¨™ã®ã‚­ãƒ³ã‚°ã®é§’ãŒç§»å‹•ã§ãã‚‹ç¯„å›²ã‚’å–å¾—ã™ã‚‹ã€‚
pub fn movedKingWalk(self: Board, position: BitBoard) BitBoard {
    const color = self.getColor(position) orelse return BitBoard.init();

    const ally_pawn = self.getBoard(color, .pawn);
    const ally_king = self.getBoard(color, .king);
    const opponent_pawn = self.getBoard(color.turn(), .pawn);
    const opponent_king = self.getBoard(color.turn(), .king);

    const ally_board = ally_pawn.unions(ally_king);
    const opponent_board = opponent_pawn.unions(opponent_king);
    const occupied_positions = ally_board.unions(opponent_board);

    const move_to = position.moveMaskedMultiple(&.{ .ne, .se, .nw, .sw });

    return move_to.excludes(occupied_positions);
}

test "ğŸ“–Board.movedKingWalk" {
    const a = std.testing.allocator;
    const board_str =
        \\........
        \\......o.
        \\.....o..
        \\........
        \\........
        \\..x.....
        \\.o......
        \\........
    ;

    const board = Board.initWithString(a, board_str);

    try board.movedKingWalk(BitBoard.initWithCoordinate(1, 1)).expect(
        \\........
        \\........
        \\........
        \\........
        \\........
        \\o.......
        \\........
        \\o.o.....
    );

    try board.movedKingWalk(BitBoard.initWithCoordinate(6, 6)).expect(
        \\.....o.o
        \\........
        \\.......o
        \\........
        \\........
        \\........
        \\........
        \\........
    );
}

/// æŒ‡å®šã—ãŸåº§æ¨™ã®ãƒãƒ¼ãƒ³ã®é§’ãŒç›¸æ‰‹ã®é§’ã‚’é£›ã³è¶Šãˆã¦ç§»å‹•ã§ãã‚‹ç¯„å›²ã‚’å–å¾—ã™ã‚‹ã€‚
pub fn movedPawnJump(self: Board, position: BitBoard) BitBoard {
    const color = self.getColor(position) orelse return BitBoard.init();

    const ally_pawn = self.getBoard(color, .pawn);
    const ally_king = self.getBoard(color, .king);
    const opponent_pawn = self.getBoard(color.turn(), .pawn);
    const opponent_king = self.getBoard(color.turn(), .king);

    const ally_board = ally_pawn.unions(ally_king);
    const opponent_board = opponent_pawn.unions(opponent_king);
    const occupied_positions = ally_board.unions(opponent_board);

    var move_jump = BitBoard.init();

    const directions = pawnDirections(color);
    for (directions) |direction| {
        const intermediate_position = position.moveMasked(direction).masks(opponent_board);

        move_jump.setUnion(intermediate_position.moveMasked(direction));
    }

    return move_jump.excludes(occupied_positions);
}

test "ğŸ“–Board.movedPawnJump" {
    const a = std.testing.allocator;
    const board_str =
        \\........
        \\........
        \\........
        \\........
        \\........
        \\..o.x...
        \\...o....
        \\........
    ;

    const board = Board.initWithString(a, board_str);

    try board.movedPawnJump(BitBoard.initWithCoordinate(3, 1)).expect(
        \\........
        \\........
        \\........
        \\........
        \\.....o..
        \\........
        \\........
        \\........
    );
}

/// æŒ‡å®šã—ãŸåº§æ¨™ã®ã‚­ãƒ³ã‚°ã®é§’ãŒç›¸æ‰‹ã®é§’ã‚’é£›ã³è¶Šãˆã¦ç§»å‹•ã§ãã‚‹ç¯„å›²ã‚’å–å¾—ã™ã‚‹ã€‚
pub fn movedKingJump(self: Board, position: BitBoard) BitBoard {
    const color = self.getColor(position) orelse return BitBoard.init();

    const ally_pawn = self.getBoard(color, .pawn);
    const ally_king = self.getBoard(color, .king);
    const opponent_pawn = self.getBoard(color.turn(), .pawn);
    const opponent_king = self.getBoard(color.turn(), .king);

    const ally_board = ally_pawn.unions(ally_king);
    const opponent_board = opponent_pawn.unions(opponent_king);
    const occupied_positions = ally_board.unions(opponent_board);

    var move_jump = BitBoard.init();

    const directions = [_]BitBoard.Direction{ .ne, .se, .nw, .sw };
    for (directions) |direction| {
        const intermediate_position = position.moveMasked(direction).masks(opponent_board);

        move_jump.setUnion(intermediate_position.moveMasked(direction));
    }

    return move_jump.excludes(occupied_positions);
}

test "ğŸ“–Board.getMoveJump" {
    const a = std.testing.allocator;
    const board_str =
        \\........
        \\........
        \\.x......
        \\..x.o...
        \\...o....
        \\..x.....
        \\........
        \\........
    ;

    const board = Board.initWithString(a, board_str);

    try board.movedKingJump(BitBoard.initWithCoordinate(3, 3)).expect(
        \\........
        \\........
        \\........
        \\........
        \\........
        \\........
        \\.o......
        \\........
    );
}

/// æŒ‡å®šã—ãŸä½ç½®ã®é§’ã‚’åè»¢ã•ã›ã‚‹
fn togglePiece(self: *Board, position: BitBoard, color: Color, piece: Piece) void {
    self.getBoardPtr(color, piece).setToggle(position);
}

/// é§’ã‚’ç§»å‹•ã•ã›ã‚‹ã€‚
/// æœ€ã‚‚ä¸Šã«åˆ°é”ã—ãŸå ´åˆã¯ã‚­ãƒ³ã‚°ã«æ˜‡æ ¼ã—ã€trueã‚’è¿”ã™ã€‚
pub fn setMovedWalk(self: *Board, position_from: BitBoard, position_to: BitBoard) void {
    const color, const piece = self.getColorPiece(position_from) orelse return;

    self.togglePiece(position_from, color, piece);

    if ((color == .white and position_to.isJoint(BitBoard.north_mask.getInverted())) or
        (color == .black and position_to.isJoint(BitBoard.south_mask.getInverted())))
    {
        self.togglePiece(position_to, color, .king);
    } else {
        self.togglePiece(position_to, color, piece);
    }
}

test "ğŸ“–Board.setMovedWalk" {
    const a = std.testing.allocator;
    const board_str =
        \\........
        \\......o.
        \\........
        \\........
        \\...o....
        \\..x.....
        \\........
        \\........
    ;

    var board = Board.initWithString(a, board_str);
    {
        board.setMovedWalk(
            BitBoard.initWithCoordinate(3, 3),
            BitBoard.initWithCoordinate(4, 4),
        );

        try board.getBoard(.white, .pawn).expect(
            \\........
            \\......o.
            \\........
            \\....o...
            \\........
            \\........
            \\........
            \\........
        );
    }

    {
        board.setMovedWalk(
            BitBoard.initWithCoordinate(6, 6),
            BitBoard.initWithCoordinate(5, 7),
        );

        try board.getBoard(.white, .king).expect(
            \\.....o..
            \\........
            \\........
            \\........
            \\........
            \\........
            \\........
            \\........
        );
    }
}

/// é§’ã‚’ç§»å‹•ã•ã›ã€é€”ä¸­ã®ç›¸æ‰‹ã®é§’ã‚’å–ã‚Šã®ãã
/// æœ€ã‚‚ä¸Šã«åˆ°é”ã—ãŸå ´åˆã¯ã‚­ãƒ³ã‚°ã«æ˜‡æ ¼ã—ã€trueã‚’è¿”ã™ã€‚
pub fn setMovedJump(self: *Board, position_from: BitBoard, position_to: BitBoard, position_jumped: BitBoard) bool {
    const color, const piece = self.getColorPiece(position_from) orelse return false;

    var is_promoted = false;
    self.togglePiece(position_from, color, piece);
    if ((color == .white and position_to.isJoint(BitBoard.north_mask.getInverted())) or
        (color == .black and position_to.isJoint(BitBoard.south_mask.getInverted())))
    {
        self.togglePiece(position_to, color, .king);
        is_promoted = true;
    } else {
        self.togglePiece(position_to, color, piece);
    }

    const color_jumped, const piece_jumped = self.getColorPiece(position_jumped) orelse return is_promoted;
    self.togglePiece(position_jumped, color_jumped, piece_jumped);

    return is_promoted;
}

test "ğŸ“–Board.setMovedJump" {
    const a = std.testing.allocator;
    const board_str =
        \\........
        \\........
        \\........
        \\........
        \\...o....
        \\..x.....
        \\........
        \\........
    ;

    var board = Board.initWithString(a, board_str);
    const result = board.setMovedJump(
        BitBoard.initWithCoordinate(3, 3),
        BitBoard.initWithCoordinate(1, 1),
        BitBoard.initWithCoordinate(2, 2),
    );

    try std.testing.expect(!result);
    try board.getBoard(.white, .pawn).expect(
        \\........
        \\........
        \\........
        \\........
        \\........
        \\........
        \\.o......
        \\........
    );
    try board.getBoard(.black, .pawn).expect(
        \\........
        \\........
        \\........
        \\........
        \\........
        \\........
        \\........
        \\........
    );
}
